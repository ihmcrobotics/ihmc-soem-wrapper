plugins {
   id 'us.ihmc.ihmc-build' version '0.15.4'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.3.3-ihmc2'


project.ext.fullVersion = version
project.ext.vcsUrl = "https://github.com/ihmcrobotics/soem"
project.ext.licenseURL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
project.ext.licenseName = "Apache License, Version 2.0"
project.ext.bintrayLicenseName = "Apache-2.0"

if(property('target') == 'JAVA')
{
	buildDir = 'build-java'
}
else
{
	buildDir = 'build-platform'
}


sourceSets {

	if(property('target') == 'JAVA')
	{
		main {
			java {
				srcDir 'swig/'
			}
		}
	}
	else
	{
		main {
			resources{
				srcDir 'swig'
				include '**/libsoemJava.so'
				exclude '**/CMakeFiles'
			}	
		}
	}
}

repositories {
  jcenter()
  mavenCentral()
  maven {
      url  "http://dl.bintray.com/ihmcrobotics/maven-release"
  }
}

dependencies {
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",

      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": project.ext.licenseURL,
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc'
			artifactId project.name
			version "$version"
			from components.java

			pom.withXml {
                            asNode().children().last() + {
                                resolveStrategy = Closure.DELEGATE_FIRST
                                name project.name
                                url project.ext.vcsUrl
                                licenses {
                                    license {
                                        name project.ext.licenseName
                                        url project.ext.licenseURL
                                        distribution 'repo'
                                    }
                                }
                                developers {
                                    developer {
                                        id 'jsmith'
                                        name 'Jesper Smith'
                                        email 'jsmith@ihmc.us'
                                    }
                                }
                            }

                            }


			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}